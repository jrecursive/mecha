
// 
// register mecha.vm.bifs.* system verbs
// 

register namespace:$ module-class:mecha.vm.bifs.RiakClientModule verbs:(get:Get put:Put delete:Delete bucket-props:BucketProps)
register namespace:$ module-class:mecha.vm.bifs.ChannelModule verbs:(subscribe:Subscribe unsubscribe:Unsubscribe publish:Publish)
register namespace:$ module-class:mecha.vm.bifs.ClientModule verbs:(client-sink:ClientSink channel-sink:ChannelSink start-all-sources:StartAllSources macro-sink:MacroSink)
register namespace:$ module-class:mecha.vm.bifs.SolrModule verbs:(solr-select:Select solr-select-iterator:SelectIterator value-count-reducer:ValueCountReducer covered-index-select:CoveredIndexSelect cardinality-reducer:CardinalityReducer field-value-count-reducer:FieldValueCountReducer sorted-accumulating-reducer:SortedAccumulatingReducer select-delete:DeleteBySelect)
register namespace:$ module-class:mecha.vm.bifs.MDBModule verbs:(materialize-pbk-stream:MaterializePBKStream stream-partition-bucket:StreamPartitionBucket partition-bucket-iterator:PartitionBucketIterator drop-partition-bucket:DropPartitionBucket global-drop-bucket:GlobalDropBucket compute-schema:ComputeSchema)
register namespace:$ module-class:mecha.vm.bifs.ETLModule verbs:(project:Project extract-riak-value:ExtractRiakValue)
register namespace:$ module-class:mecha.vm.bifs.ClusterModule verbs:(with-coverage:WithCoverage with-iterated-coverage:WithIteratedCoverage with-sorted-coverage:WithSortedCoverage warp:Warp with-cluster-coverage:WithClusterCoverage)
register namespace:$ module-class:mecha.vm.bifs.StreamModule verbs:(vector-sequencer:VectorSequencer eat:Eat limit:Limit bloom-dedupe:BloomDedupe accumulating-field-reducer:AccumulatingFieldReducer)
register namespace:$ module-class:mecha.vm.bifs.RelationalModule verbs:(sort-merge-equijoin:SortMergeEquiJoin)
register namespace:$ module-class:mecha.vm.bifs.IteratorModule verbs:(buffered-iterator:BufferedIterator)
register namespace:$ module-class:mecha.vm.bifs.SystemModule verbs:(commit:Commit splitter:Splitter repeater:Repeater invoke:Invoke)

/////////////
// globals //
/////////////

//
// spatial-select
//

#define-global spatial-select
#if ($args.filter && $args.filter != "") 
	#set ($args.logical-connector = "AND") 
#else #set ($args.logical-connector = "") #end
#if (!$args.filter) #set ($args.filter = "") #end
#if (!$args.materialize) #set ($args.materialize = "false") #end
#if (!$args.sort-field) #set ($args.sort-field = "key") #end
#if (!$args.sort-dir) #set ($args.sort-dir = "asc") #end
#set ($args.filter = $util.escapeQuotes($args.filter))

//reverted to with-sorted-coverage to support core-per-partition organization
//${root} = (covered-index-select sort-field:${args.sort-field} sort-order:${args.sort-dir} query-marker:"<<${guid}-q>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select-iterator params:(q:"((<<${guid}-q>>) AND bucket:${args.bucket}) ${args.logical-connector} ${args.filter}" sfield:${args.field} pt:"${args.lat},${args.lon}" d:${args.radius} fq:"{!bbox}" sort:"${args.sort-field} ${args.sort-dir}") materialize:${args.materialize})))

${root} = (with-sorted-coverage sort-field:${args.sort-field} sort-order:${args.sort-dir} partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select-iterator params:(q:"partition:<<${guid}-p>> AND bucket:${args.bucket} ${args.filter}" sfield:${args.field} pt:"${args.lat},${args.lon}" d:${args.radius} fq:"{!bbox}" sort:"${args.sort-field} ${args.sort-dir}") materialize:${args.materialize})))

${root} -> ${sink}

#end spatial-select

//
// facet
//

#define-global facet
#if ($args.filter && $args.filter != "") 
	#set ($args.logical-connector = "AND") 
#else #set ($args.logical-connector = "") #end
#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))

#if (!$args.mincount) #set ($args.mincount = "1") #end
#if (!$args.limit) #set ($args.limit = "-1") #end
#if (!$args.start) #set ($args.start = "0") #end
#if (!$args.sort) #set ($args.sort = "count") #end

#if (!$args.h2count) #set ($args.h2count_str = "")
#else #set ($args.h2count_str = " h2count:true") #end

#if ($args.prefix) #set ($prefix_arg = "facet.prefix:${args.prefix}")
#else #set ($prefix_arg = "") #end

#if (!$args.cardinality-only) #set ($args.cardinality-only = "false") #end

${root} = (with-coverage partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select params:(facet:true facet.limit:${args.limit} facet.field:${args.field} facet.offset:${args.start} $prefix_arg facet.sort:${args.sort} rows:0 q:"(partition:<<${guid}-p>> AND bucket:${args.bucket}) ${args.logical-connector} ${args.filter}") cardinality-only:${args.cardinality-only} partition:<<${guid}-p>>)))

#if ($args.cardinality-only == "true")
${guid}-reducer = (cardinality-reducer)
#else
${guid}-reducer = (value-count-reducer mincount:${args.mincount}${args.h2count_str})
#end

${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end facet

//
// range-facet
//

#define-global range-facet

#*
    field: field to facet
    range-field: numeric or date field to bucket facet results (_i, _d, _l, _f, _dt)
    range-start: any DateMathParser expr, e.g. NOW-1DAY or numeric value
    range-end: any DateMathParser expr, e.g. NOW or numeric value
    range-gap: any DateMathParser expr, e.g., +1HOUR or numeric value
    filter: optional pre-faceting filtering
    mincount: minimum frequency to be included in results
    
    date math: http://lucene.apache.org/solr/api/org/apache/solr/util/DateMathParser.html
*#

#if ($args.filter && $args.filter != "") 
	#set ($args.logical-connector = "AND") 
#else #set ($args.logical-connector = "") #end
#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))

#if (!$args.mincount) #set ($args.mincount = "1") #end

#if (!$args.cardinality-only) #set ($args.cardinality-only = "false") #end

${root} = (with-coverage partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select params:(facet:true facet.range:${args.range-field} facet.range.start:${args.range-start} facet.range.end:${args.range-end} facet.range.gap:${args.range-gap} facet.mincount:${args.mincount} rows:0 q:"(partition:<<${guid}-p>> AND bucket:${args.bucket}) ${args.logical-connector} ${args.filter}") partition:<<${guid}-p>>)))

${guid}-reducer = (value-count-reducer)
${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end range-facet


// 
// cardinality
//

#define-global cardinality
#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))
${root} = (#facet bucket:${args.bucket} field:${args.field} filter:"${args.filter}" start:0 limit:-1 sort:index cardinality-only:true)
${root} -> ${sink}
#end cardinality

//
// count
//

#define-global count
#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))
${root} = (with-coverage partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select params:(q:"partition:<<${guid}-p>> AND bucket:${args.bucket} ${args.filter}") count-only:true)))
${guid}-reducer = (value-count-reducer)
${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end count

//
// derive-schema
//

#define-global derive-schema
#if (!$args.max-samples) #set ($args.max-samples = "-1") #end
${root} = (with-coverage partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(compute-schema partition:<<${guid}-p>> bucket:${args.bucket} max-samples:${args.max-samples})))
${guid}-reducer = (value-count-reducer)
${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end derive-schema

//
// drop-bucket
//

#define-global drop-bucket
#if ($args.bucket)
${root} = (with-cluster-coverage host-marker:"<<${guid}-h>>" do:(warp host:<<${guid}-h>> do:(global-drop-bucket bucket:${args.bucket})))
${guid}-reducer = (value-count-reducer)
${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end
#end drop-bucket

//
// select
//

#define-global select
#if (!$args.start) #set ($args.start = "0") #end
#if (!$args.limit) #set ($args.limit = "-1") #end
#if (!$args.sort-field) #set ($args.sort-field = "key") #end
#if (!$args.sort-dir) #set ($args.sort-dir = "asc") #end
#if (!$args.materialize) #set ($args.materialize = "false") #end
#if (!$args.core) #set ($args.core = "index") #end
#if (!$args.fields) #set ($args.fields = "*") #end

#*
    If a filter is specified, generate the logical AND
    connector.
*#

#if ($args.filter && $args.filter != "") 
	#set ($args.logical-connector = "AND") 
#else #set ($args.logical-connector = "") #end
#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))

#*
    Generate the chain of:
        covered-index-select do:
            warp <host> do: 
                solr-select-iterator
*#

//reverted to with-sorted-coverage to support core-per-partition organization
//${root} = (covered-index-select sort-field:${args.sort-field} sort-order:${args.sort-dir} query-marker:"<<${guid}-q>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select-iterator params:(q:"*:*" fq:"((<<${guid}-q>>) AND bucket:${args.bucket}) ${args.logical-connector} ${args.filter}" rows:${args.limit} sort:"${args.sort-field} ${args.sort-dir}" fl:"${args.fields}") materialize:${args.materialize} core:${args.core})))

${root} = (with-sorted-coverage sort-field:${args.sort-field} sort-order:${args.sort-dir} partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select-iterator params:(q:"*:*" fq:"partition:<<${guid}-p>> AND bucket:${args.bucket} ${args.filter}" rows:${args.limit} sort:"${args.sort-field} ${args.sort-dir}") materialize:${args.materialize})))

#*
    Account for start & count (e.g., LIMIT <start>,<limit>)
*#

#set ($connecting_vertex = $root)

#*
    --- no longer necessary with "perfectly aligned" coverage ---
    ${guid}-dedupe = (bloom-dedupe num-elements:1000000 max-fp-rate:0.0001 notify:"${root}")
    ${connecting_vertex} -> ${guid}-dedupe
    #set ($connecting_vertex = "${guid}-dedupe")
*#

#if ($args.start != "0")
${guid}-start = (eat count:${args.start})
${connecting_vertex} -> ${guid}-start
#set ($connecting_vertex = "${guid}-start")
#end

#if ($args.limit != "-1")
${guid}-limit = (limit count:${args.limit} notify:"${root}")
${connecting_vertex} -> ${guid}-limit
#set ($connecting_vertex = "${guid}-limit")
#end

${connecting_vertex} -> ${sink}
#end select

//
// select
//

#define-global select-set
#if (!$args.start) #set ($args.start = "0") #end
#if (!$args.limit) #set ($args.limit = "-1") #end
#if (!$args.sort-field) #set ($args.sort-field = "key") #end
#if (!$args.sort-dir) #set ($args.sort-dir = "asc") #end
#if (!$args.materialize) #set ($args.materialize = "false") #end
#if (!$args.core) #set ($args.core = "index") #end
#if (!$args.fields) #set ($args.fields = "*") #end

#if ($args.filter && $args.filter != "") 
	#set ($args.logical-connector = "AND") 
#else 
    #set ($args.logical-connector = "") 
#end

#if (!$args.filter) #set ($args.filter = "") #end
#set ($args.filter = $util.escapeQuotes($args.filter))

${root} = (with-coverage partition-marker:"<<${guid}-p>>" host-marker:"<<${guid}-h>>" bucket:${args.bucket} do:(warp host:<<${guid}-h>> do:(solr-select params:(q:"*:*" fq:"(partition:<<${guid}-p>> AND bucket:${args.bucket}) ${args.logical-connector} ${args.filter}" start:${args.start} rows:${args.limit} sort:"${args.sort-field} ${args.sort-dir}" fl:"${args.fields}") materialize:${args.materialize} core:${args.core})))
// ${guid}-dedupe = (bloom-dedupe num-elements:1000000 max-fp-rate:0.001)
${guid}-reducer = (sorted-accumulating-reducer sort-field:${args.sort-field} sort-dir:${args.sort-dir})

// ${root} -> ${guid}-dedupe
// ${guid}-dedupe -> ${guid}-reducer
${root} -> ${guid}-reducer

${guid}-reducer -> ${sink}

#end select-set


//
// delete-by-select
//

#define-global delete-by-select
#if (!$args.core) #set ($args.core = "index") #end

#*
    "filter" MUST be specified for delete-by-select to do anything.  If one
    weren't specified, it would simply delete an entire bucket row by row.  To
    delete an entire bucket, use "#drop-bucket".
*#

#if ($args.filter)
    #set ($args.filter = $util.escapeQuotes($args.filter))
    ${root} = (with-cluster-coverage host-marker:"<<${guid}-h>>" do:(warp host:<<${guid}-h>> do:(select-delete q:"bucket:${args.bucket} AND ${args.filter}" core:${args.core})))
    ${root} -> ${sink}
#end
#end delete-by-select

//
// join
// 

#define-global join
${root} = (repeater start:true to:(${guid}-left ${guid}-right ${guid}-join))
${guid}-left = (#${args.left.type} #foreach($entry in ${args.left.params.entrySet()}) ${entry.getKey()}:"${entry.getValue()}" #end sort-field:${args.left.join-field} sort-dir:asc)
${guid}-right = (#${args.right.type} #foreach($entry in ${args.right.params.entrySet()}) ${entry.getKey()}:"${entry.getValue()}" #end sort-field:${args.right.join-field} sort-dir:asc)
${guid}-left-iter = (buffered-iterator)
${guid}-right-iter = (buffered-iterator)
${guid}-join = (sort-merge-equijoin left:(input:${guid}-left-iter field:${args.left.join-field}) right:(input:${guid}-right-iter field:${args.right.join-field}))
${guid}-projection = (project fields:(#foreach($val in ${args.project}) $val #end))
${guid}-left -> ${guid}-left-iter
${guid}-right -> ${guid}-right-iter
${guid}-left-iter -> ${guid}-join
${guid}-right-iter -> ${guid}-join
${guid}-join -> ${guid}-projection
${guid}-projection -> ${sink}
#end join

//
// cluster-wide commit
//

#define-global commit
${root} = (with-cluster-coverage do:(warp host:<<host>> do:(commit)))
${root} -> ${sink}
#end commit


//
// cluster-wide count
//  takes parameter "type":
//      "bucket": count for all records, broken down by bucket, globally
//      "partition": count for all records, broken down by partition, globally
//      "custom": requires additional parameter "field"; results are tallied as
//                a global count (which includes all replicas) of all distinct values
//                for the field specified in "field".
//

#define-global global-count
#if (!${args.type}) #set (${args.type} = "bucket") #end
#if (${args.type} == "bucket") #set ($facet_field = "bucket") #end
#if (${args.type} == "partition") #set ($facet_field = "partition") #end
#if (${args.type} == "custom") #set ($facet_field = ${args.field}) #end
#if ($facet_field)
${root} = (with-cluster-coverage host-marker:"<<${guid}-h>>" do:(warp host:<<${guid}-h>> do:(solr-select params:(facet:true facet.field:${facet_field} rows:0 q:"*:*"))))
${guid}-reducer = (value-count-reducer)
${root} -> ${guid}-reducer
${guid}-reducer -> ${sink}
#end
#end global-count

//
// system-facet is used for monitoring
//
// required: host, filter, sort, limit
// optional: fields

#define-global system-select
#if (!$args.fields) #set ($args.fields = "*") #end
${root} = (warp host:${args.host} do:(solr-select params:(q:"${args.filter}" sort:"${args.sort}" rows:${args.limit} fl:"${args.fields}") core:system))
${root} -> ${sink}
#end system-select

//
// local-system-select is used for monitoring
//
// required: host, filter, sort, limit
// optional: fields

#define-global local-system-select
#if (!$args.fields) #set ($args.fields = "*") #end
${root} = (solr-select params:(q:"${args.filter}" sort:"${args.sort}" rows:${args.limit} fl:"${args.fields}") core:system)
${root} -> ${sink}
#end local-system-select

//
// system-facet is used for monitoring
//
// required: host, field, sort, filter
// optional: prefix
//

#define-global system-facet
#if ($args.prefix) #set ($prefix_arg = "facet.prefix:${args.prefix}")
#else #set ($prefix_arg = "") #end
#if (!$args.fields) #set ($args.fields = "*") #end
#set ($args.filter = $util.escapeQuotes($args.filter))
${root} = (warp host:${args.host} do:(solr-select params:(facet:true facet.limit:-1 facet.field:${args.field} facet.mincount:1 $prefix_arg facet.sort:${args.sort} rows:0 q:"${args.filter}" fl:"${args.fields}") core:system))
${root} -> ${sink}
#end system-facet

//
// host-facet is mainly used for admin ui browser; "metadata faceting"
//
// required: field
// optional: prefix, filter, core, sort
//

#define-global host-facet
    #if ($args.prefix) 
        #set ($prefix_arg = "facet.prefix:${args.prefix}")
    #else 
        #set ($prefix_arg = "") 
    #end
    #if (!$args.core) #set ($args.core = "index") #end
    #if (!$args.fields) #set ($args.fields = "*") #end
    #if (!$args.sort) #set ($args.sort = "count") #end
    #if (!$args.filter) 
        #set ($args.filter = "*:*")
    #else 
        #set ($args.filter = $util.escapeQuotes($args.filter))
    #end
    ${root} = (with-cluster-coverage host-marker:"<<${guid}-h>>" do:(warp host:<<${guid}-h>> do:(solr-select params:(facet:true facet.limit:-1 facet.field:${args.field} facet.mincount:1 $prefix_arg facet.sort:${args.sort} rows:0 q:"${args.filter}" fl:"${args.fields}") core:${args.core})))
    ${guid}-accum = (accumulating-field-reducer field:"$host")
    ${root} -> ${guid}-accum
    ${guid}-accum -> ${sink}
#end host-facet

//
// local-system-facet is used for monitoring
//
// required: host, field, sort, filter
// optional: prefix
//

#define-global local-system-facet
#if ($args.prefix) #set ($prefix_arg = "facet.prefix:${args.prefix}")
#else #set ($prefix_arg = "") #end
#if (!$args.fields) #set ($args.fields = "*") #end
#set ($args.filter = $util.escapeQuotes($args.filter))
${root} = (solr-select params:(facet:true facet.limit:-1 facet.field:${args.field} facet.mincount:1 $prefix_arg facet.sort:${args.sort} rows:0 q:"${args.filter}" fl:"${args.fields}") core:system)
${root} -> ${sink}
#end local-system-facet

//
// "real-time" global cluster metrics, errors & logging
//

#define-global subscribe-global
#if ($args.channel)
    (with-cluster-coverage host-marker:"<<h>>" do:(warp host:<<h>> do:(subscribe channel:${args.channel})))
#end
#end subscribe-global

#define-global publish-global
#if ($args.channel && $args.data)
    (with-cluster-coverage host-marker:"<<${guid}-h>>" do:(warp host:<<${guid}-h>> do:(publish channel:${args.channel} data:${args.data})))
#end
#end publish-global

//
// get bucket riak properties
//

#define-global bucket-props
${root} = (bucket-props bucket:${args.bucket})
${root} -> ${sink}
#end bucket-props

